plugins {
	id 'fabric-loom' version "0.10-SNAPSHOT" // To use chocoloom, change the version to a commit hash
	id 'maven-publish'
}
def ENV = System.getenv()
def minecraft_version = ENV.MC_VERSION ? ENV.MC_VERSION : "1.8.9"
def old_version = !"1.13.2 1.12.2 1.11.2 1.10.2 1.9.4 1.8.9 1.7.10".contains(minecraft_version);
def lwjglVersion = System.getProperty("os.name").toLowerCase().contains("mac") ? "2.9.1" : "2.9.0"

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {
	mavenLocal()
	maven {
		name = "legacy-fabric"
		url = "https://maven.legacyfabric.net"
	}
}

// Comment out this block if you're using Chocoloom
// Comment when using loom 0.10
// minecraft {
// 	intermediaryUrl = {
// 		return "https://maven.legacyfabric.net/net/fabricmc/intermediary/" + it + "/intermediary-" + it + "-v2.jar";
// 	}
// }
loom {
// Uncomment when using loom 0.10
	setIntermediaryUrl('https://maven.legacyfabric.net/net/fabricmc/intermediary/%1$s/intermediary-%1$s-v2.jar');
}

dependencies {
	if (old_version) {
		implementation 'org.apache.logging.log4j:log4j-core:2.17.1'
		implementation 'org.apache.logging.log4j:log4j-api:2.17.1'

		implementation "org.lwjgl.lwjgl:lwjgl_util:${lwjglVersion}"
		implementation "org.lwjgl.lwjgl:lwjgl:${lwjglVersion}"
		implementation "org.lwjgl.lwjgl:lwjgl-platform:${lwjglVersion}"
	}

	minecraft "com.mojang:minecraft:${minecraft_version}"
	mappings "net.fabricmc:yarn:${minecraft_version}+build.local:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
}

if (old_version) {
	configurations.all {
		resolutionStrategy {
			dependencySubstitution {
				substitute module('org.lwjgl.lwjgl:lwjgl_util:2.9.1-nightly-20130708-debug3') with module("org.lwjgl.lwjgl:lwjgl_util:${lwjglVersion}")
				substitute module('org.lwjgl.lwjgl:lwjgl:2.9.1-nightly-20130708-debug3') with module("org.lwjgl.lwjgl:lwjgl:${lwjglVersion}")
			}
			force "org.lwjgl.lwjgl:lwjgl-platform:${lwjglVersion}"
		}
	}
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"
	if (JavaVersion.current().isJava9Compatible()) it.options.release = 8
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	// select the repositories you want to publish to
	repositories {
		// uncomment to publish to the local maven
		// mavenLocal()
	}
}
